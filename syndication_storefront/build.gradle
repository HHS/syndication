buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
        maven { url "http://ctacdev.com:8080/artifactory/central" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:2.14.1"
        classpath "org.grails.plugins:hibernate5:${gormVersion-".RELEASE"}"
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.17"
        classpath "com.ctacorp.syndication:syndication-jenkinsfile:1.3.0"
    }
}

plugins {
    id "net.saliman.cobertura" version "2.4.0"
}

version "2.0.4"
group "com.ctacorp.syndication"

apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "war"
apply plugin: "org.grails.grails-web"
apply plugin: "org.grails.grails-gsp"
apply plugin: "asset-pipeline"
apply plugin: "net.saliman.cobertura"
apply plugin: "com.gorylenko.gradle-git-properties"
apply plugin: "codenarc"
apply plugin: "com.ctacorp.syndication-jenkinsfile"

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
    maven { url "http://ctacdev.com:8080/artifactory/central" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    provided "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-dependencies"
    compile "org.grails:grails-web-boot"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:scaffolding"
    compile "org.grails.plugins:hibernate5"
    compile "org.hibernate:hibernate-core:5.1.3.Final"
    compile "org.hibernate:hibernate-ehcache:5.1.3.Final"
    console "org.grails:grails-console"
    profile "org.grails.profiles:web"
    runtime "com.bertramlabs.plugins:asset-pipeline-grails:2.14.1"
    runtime "com.h2database:h2"
    testCompile "org.grails:grails-plugin-testing"
    testCompile "org.grails.plugins:geb"
    testRuntime "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1"
    testRuntime "net.sourceforge.htmlunit:htmlunit:2.18"

    compile "org.elasticsearch:elasticsearch:5.6.1"
    compile "com.ctacorp:syndication-elasticsearch-plugin:1.1.0"
    compile "com.ctacorp:syndication-model:3.0.0"
    compile "com.ctacorp.syndication:content-extraction-services:3.0.0"

    compile "org.grails.plugins:spring-security-core:3.1.1"
    compile "org.grails.plugins:recaptcha:3.1.0"
    compile "org.grails.plugins:grails-markdown:3.0.0"
    compile "org.grails:grails-datastore-rest-client:5.0.0.RC2"
    // plugins for the compile step

    compile "org.grails.plugins:quartz:2.0.9"

    //Email Support
    compile "org.grails.plugins:mail:2.0.0.RC6"

    // Recaptcha plugin
    compile 'org.grails.plugins:recaptcha:3.1.0'

    //pagination
    compile "org.grails.plugins:remote-pagination:0.5.0"

    // plugins needed at runtime but not for compilation
    runtime "org.grails.plugins:database-migration:3.0.0"
    runtime "org.grails.plugins:ajax-tags:1.0.0.RC1"
    runtime "mysql:mysql-connector-java:5.1.38"
    compile 'com.ctacorp:syndication-commons:3.0.0'

    compile 'io.searchbox:jest:2.0.0'

    //Testing ---------------------------------------------------------------
    testCompile "org.grails.plugins:build-test-data:3.0.1"

    runtime ('org.grails.plugins:x-frame-options:1.1.2')
}

bootRun {
    jvmArgs('-Dspring.output.ansi.enabled=always')
}


assets {
    minifyJs = true
    minifyCss = true
}

cobertura {
    coverageFormats = ['html', 'xml']
    coverageExcludes = [
            ".*ApplicationLoader",
            ".*BootStrap.*",
            ".*Application",
            ".*ApplicationController",
            ".*UrlMappings.*"
    ]
}

codenarc {
    toolVersion = "0.26.0"
    configFile = rootProject.file("config/codenarc/codenarc.groovy")
    ignoreFailures = true
}

gitProperties {
    dateFormat = "yyyy-MM-dd HH:mm"
    dateFormatTimeZone = "UTC"
}

processResources.doLast {

    def buildInfoFile = new File('build/resources/main/META-INF/grails.build.info')
    def buildInfoProps = new Properties()
    def gitInfoFile = new File('build/resources/main/git.properties')
    def gitInfoProps = new Properties()

    buildInfoFile.withInputStream {
        buildInfoProps.load(it)
    }

    buildInfoProps.setProperty('info.app.build.date', new Date().format("yyyy-MM-dd HH:mm"))

    gitInfoFile.withInputStream {
        gitInfoProps.load(it)
    }

    buildInfoProps.putAll(gitInfoProps)

    def applicationYamlFile = new File('build/resources/main/application.yml')
    applicationYamlFile.append('\n\n')

    buildInfoProps.each { k,v ->
        applicationYamlFile.append("${k}: '${v}'\n")
    }
}
